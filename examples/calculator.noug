# a simple Reverse Polish Notation calculator

print()
print("Welcome to the Nougaro calculator!")
print("This calculator uses the Reverse Polish Notation.")
print("Examples:")
print()
print("  1 + 1 will be 1 1 +")
print("  1 ^ 3 will be 1 3 ^")
print("  (1 + 1) * (12 + 13) will be 1 1 + 12 13 + *")
print()

while True then
    var calcul = input("calc> ")
    var should_continue = False
    
    if calcul == "exit" then break
    
    # tokenize
    var tokens = []
    var floating_point = 0
    for char in calcul then
        var old_floating_point = floating_point
        var current_tok = char
        if char in "0123456789" then
            if len(tokens) != 0 and not is_num(tokens(-1)) then
                if tokens(-1) == "-" then
                    pop(tokens, -1)
                    append(tokens, -int(char))
                elif tokens(-1) == '.' then
                    pop(tokens, -1)
                    var floating_point += 1
                    if tokens(-1) >= 0 then
                        replace(tokens, -1, float(tokens(-1)) + float(char)/10)
                    else
                        replace(tokens, -1, float(tokens(-1)) - float(char)/10)
                    end
                else
                    append(tokens, int(char))
                end
            elif len(tokens) != 0 then  # last token is num
                if tokens(-1) >= 0 then
                    if is_int(tokens(-1)) then
                        replace(tokens, -1, tokens(-1)*10 + int(char))
                    else  # float
                        var floating_point += 1
                        replace(tokens, -1, round(tokens(-1) + float(char)/round(10 ^ floating_point),\
                                                               floating_point)\
                               )
                    end
                else
                    if is_int(tokens(-1)) then
                        replace(tokens, -1, tokens(-1)*10 - int(char))
                    else  # float
                        var floating_point += 1
                        replace(tokens, -1, round(tokens(-1) - float(char)/(10 ^ floating_point),\
                                                               floating_point)\
                               )
                    end
                end
            else
                append(tokens, int(char))
            end
        elif char == '.' then
            append(tokens, '.')
        elif char == " " then
            append(tokens, "space")
        elif char in "+-*/^" then
            append(tokens, char)
        else
            print("Error: illegal char " + char)
            var should_continue = True
            break
        end
        if old_floating_point == floating_point then var floating_point = var old_floating_point = 0
    end
        
    if should_continue then continue
    
    # parse
    var stack = []
    for token in tokens then
        if is_num(token) then
            append(stack, token)
        elif token == "space" then
            void()
        else
            if len(stack) >= 2 then
                if token == "+" then
                    var result = stack(-2) + stack(-1)
                elif token == "-" then
                    var result = stack(-2) - stack(-1)
                elif token == '/' then
                    if len(stack) != 0 and stack(-1) == 0 then
                        print("Error: can not divide by 0")
                        var should_continue = True
                        break
                    end
                    var result = stack(-2) / stack(-1)
                elif token == "*" then
                    var result = stack(-2) * stack(-1)
                elif token == "^" then
                    var result = stack(-2) ^ stack(-1)
                else
                    print("Error")
                    var should_continue = True
                    break
                end
                pop(stack, -1)
                pop(stack, -1)
                append(stack, result)
            else
                print("Error")
                var should_continue = True
                break
            end
        end
    end
    
    if should_continue then continue
    
    if len(stack) != 1 then
        print("Error: more than one result after execution, consider check your input.")
        continue
    end
    
    print(stack(0))
end
